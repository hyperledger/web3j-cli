
apply plugin: 'com.diffplug.spotless'
apply plugin: "de.undercouch.download"

task downloadJavaLicense(type: Download) {
    src 'https://raw.githubusercontent.com/web3j/build-tools/master/gradle/spotless/java.license'
    dest new File("$rootDir/gradle/spotless",'java.license')
    quiet true
    onlyIfModified true
}

task downloadFormatterProperties(type: Download) {
    src 'https://raw.githubusercontent.com/web3j/build-tools/master/gradle/spotless/formatter.properties'
    dest new File("$rootDir/gradle/spotless",'formatter.properties')
    quiet true
    onlyIfModified true
}

spotless {
    java {
        // This path needs to be relative to each project
        target fileTree('.') {
            include '**/src/*/java/**/*.java'
            exclude '**/.gradle/**'
            exclude '**/generated/**'
            exclude '**/build/install/**'
        }
        removeUnusedImports()
        googleJavaFormat("1.17.0").aosp()
        importOrder 'java', '', 'org.web3j', '\\#'
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile "$rootDir/gradle/spotless/java.license"
    }
    kotlin {
        // This path needs to be relative to each project
        target fileTree('.') {
            include '**/*.kt'
            exclude '**/.gradle/**'
            exclude '**/build/install/**'
        }
        ktlint('0.49.1')
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile "$rootDir/gradle/spotless/java.license"
    }
    groovyGradle {
        target '*.gradle'
        greclipse().configFile("$rootDir/gradle/spotless/formatter.properties")
        endWithNewline()
        indentWithSpaces(4)
    }
}

tasks.named('spotlessJava') {
    dependsOn downloadJavaLicense, downloadFormatterProperties
    dependsOn tasks.named('compileJava'), tasks.named('compileTestJava'), tasks.named('processResources'), tasks.named('processTestResources'), tasks.named('startScripts'), tasks.named('startShadowScripts')
    mustRunAfter tasks.named('spotlessGroovyGradle'), tasks.named('test'), tasks.named('jacocoTestReport')
}

tasks.named('spotlessKotlin') {
    dependsOn downloadJavaLicense, downloadFormatterProperties
    dependsOn tasks.named('compileJava'), tasks.named('compileTestJava'), tasks.named('processResources'), tasks.named('processTestResources'), tasks.named('spotlessJava'), tasks.named('startScripts'), tasks.named('startShadowScripts')
    mustRunAfter tasks.named('spotlessGroovyGradle'), tasks.named('test'), tasks.named('jacocoTestReport')
}

tasks.named('spotlessCheck') {
    dependsOn downloadJavaLicense, downloadFormatterProperties, tasks.named('spotlessJava'), tasks.named('spotlessKotlin'), tasks.named('spotlessGroovyGradle')
    mustRunAfter tasks.named('jacocoTestReport'), tasks.named('test')
}

